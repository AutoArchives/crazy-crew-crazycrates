plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version("$shadowVersion")
}

group = "${rootProject.group}.core"
version = rootProject.version

dependencies {
    // Provided by Mojang/Paper/Fabric
    compileOnly("net.kyori:adventure-api:$advVersion")
    compileOnly("net.kyori:adventure-text-minimessage:$advVersion")
    compileOnly("com.google.code.gson:gson:$gsonVersion")
}

base {
    archivesName = "${rootProject.name}-${project.name}"
}

java {
    withJavadocJar()
    withSourcesJar()
}

assemble {
    dependsOn(shadowJar)
}

shadowJar {
    archiveBaseName = "${rootProject.name}-${project.name}"

    archiveClassifier = ''

    mergeServiceFiles()

    [
      "org.yaml.snakeyaml",
      "org.simpleyaml",
      "ch.jalu"
    ].forEach {relocate it, "libs.$it" }
}

javadoc {
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
    title = "${rootProject.name}-${project.version} API"
}