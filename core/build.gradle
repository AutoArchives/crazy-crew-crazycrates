plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version("$shadowVersion")
}

group = "${rootProject.group}.core"
version = rootProject.version

dependencies {
    compileOnly("net.kyori:adventure-api:$advVersion")
    compileOnly("net.kyori:adventure-text-minimessage:$advVersion")

    compileOnly "com.google.code.gson:gson:$gsonVersion"

    implementation("com.github.Carleslc.Simple-YAML:Simple-Yaml:$yamlVersion") {
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
}

base {
    archivesName = "${rootProject.name}-${project.name}"
}

java {
    withJavadocJar()
    withSourcesJar()
}

assemble {
    dependsOn(shadowJar)
}

shadowJar {
    archiveBaseName = "${rootProject.name}-${project.name}"

    archiveClassifier = ''

    mergeServiceFiles()

    exclude('META-INF/LICENSE', 'META-INF/LICENSE.txt')

    [
      "org.simpleyaml",
      "org.yaml.snakeyaml"
    ].forEach {relocate it, "libs.$it" }
}

javadoc {
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
    title = "${rootProject.name}-${project.version} API"
}