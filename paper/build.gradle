plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version "$paperweightVersion"

    id 'com.github.johnrengelman.shadow' version "$shadowVersion"
}

group = "${rootProject.group}.paper"
version = rootProject.version

repositories {
    flatDir { dirs 'libs' }
}

dependencies {
    compileOnly project(':core')

    compileOnly fileTree(dir: 'libs', include: '*.jar')

    compileOnly("me.clip:placeholderapi:$papiVersion")
    compileOnly("dev.triumphteam:triumph-cmd-bukkit:$triumphCmdVersion")
    compileOnly("com.github.decentsoftware-eu:decentholograms:$decentHologramsVersion")

    compileOnly("ch.jalu:configme:$configVersion")

    implementation("cloud.commandframework:cloud-paper:$cloudVersion")
    implementation("org.bstats:bstats-bukkit:$bstatsVersion")
    implementation("de.tr7zw:item-nbt-api:$nbtVersion")

    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:$paperVersion")
}

base {
    archivesName = "${rootProject.name}-${project.name}"
}

assemble {
    dependsOn reobfJar
}

reobfJar {
    outputJar = file("$buildDir/libs/${rootProject.name}-${project.name}-${project.version}.jar")
}

shadowJar {
    [
            "ch.jalu",
            "de.tr7zw",
            "org.bstats",
            "cloud.commandframework"
    ].forEach { relocate it, "libs.$it" }
}

processResources {
    filesMatching('plugin.yml') {
        expand(
                'name': rootProject.name,
                'group': project.group,
                'version': project.version,
                'authors': authors,
                'description': project.properties['description'],
                'website': website
        )
    }
}