plugins {
    id 'io.papermc.paperweight.userdev' version "$paperweightVersion"
}

group = "${rootProject.group}.paper"
version = rootProject.version

repositories {
    flatDir { dirs 'libs' }
}

dependencies {
    compileOnly project(':core')

    compileOnly fileTree(dir: 'libs', include: '*.jar')

    compileOnly("me.clip:placeholderapi:$papiVersion")
    compileOnly("dev.triumphteam:triumph-cmd-bukkit:$triumphVersion")
    compileOnly("com.github.decentsoftware-eu:decentholograms:$decentHologramVersion")

    implementation("org.bstats:bstats-bukkit:$bStatsVersion")
    implementation("de.tr7zw:item-nbt-api:$nbtVersion")

    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:$paperVersion")
}

base {
    archivesName = "${rootProject.name}-${project.name}"
}

compileJava {
    dependsOn ":core:shadowJar"
}

assemble {
    dependsOn reobfJar
    doLast {
        delete fileTree(dir: "$buildDir", include: '**/*-dev*.jar')
    }
}

reobfJar {
    outputJar = file("$buildDir/libs/${rootProject.name}-${project.name}-${project.version}.jar")
}

processResources {
    filesMatching('paper-plugin.yml') {
        expand(
                'name': rootProject.name,
                'group': project.group,
                'version': project.version,
                'description': rootProject.description
        )
    }
}